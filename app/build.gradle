apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    repositories {
        jcenter()
    }

    def hockeyDebugKey
    def hockeyStageKey
    def hockeyLiveKey
    def buildCode = 1
    def keystore
    def keystorePassword
    def alias
    def password

    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties();
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        hockeyDebugKey = properties.getProperty('hockey.debug.key');
        hockeyStageKey = properties.getProperty('hockey.stage.key');
        hockeyLiveKey = properties.getProperty('hockey.live.key');
        keystore = file(properties.getProperty('keystore'))
        keystorePassword = properties.getProperty('keystore.password')
        alias = properties.getProperty('key.alias')
        password = properties.getProperty('key.password')
    } else {
        hockeyDebugKey = System.getenv('HOCKEY_DEBUG_KEY');
        hockeyStageKey = System.getenv('HOCKEY_STAGE_KEY');
        hockeyLiveKey = System.getenv('HOCKEY_LIVE_KEY');
        buildCode = System.getenv('CIRCLE_BUILD_NUM').toInteger()
        keystore = file(System.getenv("KEYSTORE"))
        keystorePassword = System.getenv("KEYSTORE_PASSWORD")
        alias = System.getenv("KEY_ALIAS")
        password = System.getenv("KEY_PASSWORD")
    }


    defaultConfig {
        applicationId "com.dentacoin.dentacare"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode buildCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
        }
        release {
            storeFile keystore
            storePassword keystorePassword
            keyAlias alias
            keyPassword password
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            resValue "string", "hockey_app_id", hockeyDebugKey
        }
        staging {
            applicationIdSuffix ".stage"
            resValue "string", "hockey_app_id", hockeyStageKey
        }
        live {
            resValue "string", "hockey_app_id", hockeyLiveKey
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:design:26+'
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'net.hockeyapp.android:HockeySDK:4.1.4'
    compile 'com.facebook.fresco:fresco:1.4.0'
    testCompile 'junit:junit:4.12'
}
